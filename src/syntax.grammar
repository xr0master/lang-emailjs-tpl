//!top

@top Template { (entity | PlainText)* }

//!dialects

@dialects { noMatch }

//!entity

entity[@isGroup=Entity] {
  Variable |
  Section
}

@skip { space | Comment }

//!Variable  

Variable {
  OpenTag (expression* | ListTag) CloseTag |
  OpenUnsafeTag expression* CloseUnsafeTag
}

//! Section

Section {
  OpenSection (entity | PlainText)* CloseSection
}

OpenSection[closedBy=CloseSection] {
  StartSectionTag expression* EndSectionTag
}

CloseSection[openedBy=OpenSection] {
  CloseSectionTag expression* EndSectionTag
}

//!expression

expression[@isGroup=Expression] {
  Identifier |
  ChainedIdentifier
}

//!comment

@skip {} {
  Comment {
    "{{!" (commentContent | commentNewline)* commentEnd
  }
}

@local tokens {
  commentEnd { "}}" }
  commentNewline { "\n" }
  @else commentContent
}

@tokens {
  PlainText { ![{] PlainText? | "{" (@eof | ![{#&!^] PlainText?) }

  Identifier { $[a-zA-Z_\-0-9]+ }
  ChainedIdentifier { Identifier ("." Identifier)+ }
  
  OpenTag[closedBy=CloseTag] { "{{" }
  CloseTag[openedBy=OpenTag] { "}}" }
  OpenUnsafeTag[closedBy=CloseUnsafeTag] { "{{{" }
  CloseUnsafeTag[openedBy=OpenUnsafeTag] { "}}}" }

  ListTag { "." }
  StartSectionTag[closedBy=EndSectionTag] { "{{#" | "{{^" }
  CloseSectionTag[closedBy=EndSectionTag] { "{{/" }
  EndSectionTag[openedBy=CloseSectionTag,openedBy=StartSectionTag] { "}}" }

  space { $[ \t\n\r]+ }

  @precedence {space, PlainText, ListTag, ChainedIdentifier, Identifier }
}

@detectDelim